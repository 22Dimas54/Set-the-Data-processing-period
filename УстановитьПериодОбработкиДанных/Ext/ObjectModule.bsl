
#Область ПрограммныйИнтерфейс

// Очищает записи в регистре сведений 'сибиарДанныеДляРегламентнойРаботы'.
Процедура Очистить() Экспорт
	
	НаборЗаписей = РегистрыСведений.сибиарДанныеДляРегламентнойРаботы.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Записывает значения в регистр сведений 'сибиарДанныеДляРегламентнойРаботы' и устанавливает дату записи файла в константу.
// 
// Параметры:
//  ДатаНачала    - Дата      - Дата начала периода для записи в регистр сведений
//  ДатаОкончания - Дата      - Дата окончания периода для записи в регистр сведений
//  Признак       - Не указан - Признак, который также записывается в регистр сведений
Процедура ЗаписатьЗначения(ДатаНачала, ДатаОкончания, Признак) Экспорт
	
	Попытка
		Запись = РегистрыСведений.сибиарДанныеДляРегламентнойРаботы.СоздатьМенеджерЗаписи();
		Запись.ДатаНачала = ДатаНачала;
		Запись.ДатаОкончания = ДатаОкончания;
		Запись.Признак = Признак;
		Запись.Записать();
		Константы.сибиарДатаЗаписиФайла.Установить(ТекущаяДатаСеанса());
	Исключение
		ВызватьИсключение "Не удалось записать данные, возможно у Вас нет прав! Обратитесь к администратору!" + ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает последнюю дату закрытия бухгалтерской отчетности, которая была завершена
// 
// Возвращаемое значение:
// Дата - Последняя дата закрытия бухгалтерской отчетности
Функция ПолучитьПоследнююДатуЗакрытияБухгалтерскойОтчетности() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ЗакрытиеМесяца.Дата) КАК Дата
	|ИЗ
	|	БизнесПроцесс.ЗакрытиеМесяца КАК ЗакрытиеМесяца
	|ГДЕ
	|	ЗакрытиеМесяца.Завершен = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.Дата;
	
КонецФункции

// Возвращает набор записей из регистра сведений 'сибиарДанныеДляРегламентнойРаботы', содержащий подготовленные данные для регламентной работы.
// 
// Возвращаемое значение:
// НаборЗаписей - Данные для регламентной работы
Функция ПодготовленныеДанные() Экспорт
	
	НаборЗаписей = РегистрыСведений.сибиарДанныеДляРегламентнойРаботы.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	Возврат НаборЗаписей;
	
КонецФункции

#КонецОбласти
